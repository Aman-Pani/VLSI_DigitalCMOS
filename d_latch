.title d_latch

.lib "/home/fenix/vlsi/eda-technology/sky130/models/spice/models/sky130.lib.spice ff"

.global vdd gnd
.temp 27

Vsup vdd gnd dc 1.8
Vin1 d gnd pulse 0 1.8 1n 10p 10p 3n 6n
Vin2 ck gnd pulse 0 1.8 2.5n 10p 10p 6n 12n
XDLatch d ck q d_latch


.subckt d_latch d ck q
xi0 d n2 inverter
xi1 n0 q inverter
xi2 q n1 inverter
xi3 ck  clkb inverter
xi4 clkb clk inverter
xi5 n2 n0 clkb clk TX_Gate
xi6 n1 n0 clk clkb TX_Gate
.ends d_latch

.subckt inverter input output 
xm0 output input vdd vdd sky130_fd_pr__pfet_01v8 w=3 l=0.18 m=1
xm1 output input gnd gnd sky130_fd_pr__nfet_01v8 w=1 l=0.18 m=1
.ends inverter

.subckt TX_Gate input output ckb ck
xm0 input ckb output vdd sky130_fd_pr__pfet_01v8 w=1 l=0.18 m=1
xm1 input ck output gnd sky130_fd_pr__nfet_01v8 w=1 l=0.18 m=1
.ends TX_Gate

.tran 5p 30n

.control
run
set color0=white
set color1=black
set color2=green
set color3=black
set color4=blue
set xbrushwidth=3
plot v(d)+4 v(ck)+2 v(q)
.endc
.end


